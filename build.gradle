plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.0"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

repositories {
    mavenCentral()
}

configurations {
    invoker
}

dependencies {
    implementation("com.google.cloud.functions:functions-framework-api:1.0.4")
    invoker("com.google.cloud.functions.invoker:java-function-invoker:1.1.0")
}

tasks.register("runFunction", JavaExec) {
    main = "com.google.cloud.functions.invoker.runner.Invoker"
    classpath(configurations.invoker)
    inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
    args(
            "--target", project.findProperty("run.functionTarget") ?: "",
            "--port", project.findProperty("run.port") ?: 8080
    )
    doFirst {
        args("--classpath", files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
    }
}
